#!/usr/bin/ruby

# Tests for the Number `is_almost_prime(n,k)` method.

func A242786(n) {
    for (var p = 2; true; p.next_prime!) {
        var v = (p**n + 1)
        v.is_almost_prime(n) || next
        return p
    }
}

assert_eq(
    A242786.map(1..11),
    %n[2, 3, 3, 43, 7, 41, 23, 643, 17, 557, 251],
)

#assert_eq(A242786(21), 1151)

func A241793(n) {
    for k in (1..Inf) {
        var b = bigomega(k)*n
        var v = (k**n - 1)
        is_almost_prime(v, b) || next
        return k
    }
}

assert_eq(
    A241793.map(1..16),
    %n[3, 34, 5, 15, 17, 55, 79, 5, 53, 23, 337, 13, 601, 79, 241, 41],
)

assert_eq(A241793(24), 79)

func A280005(n) {
    for(var p = 2; true; p.next_prime!) {
        var v = (p**n + 1)
        v.is_prob_squarefree(1e4) || next
        v.is_almost_prime(n) || next
        if (v.is_squarefree) {
            return p
        }
    }
}

assert_eq(
    A280005.map(1..10),
    %n[2, 3, 13, 43, 73, 47, 457, 1697, 109, 8161]
)

func A281940(n) {
    for k in (1..Inf) {
        var v = (k**n + 1)
        v.is_prob_squarefree(1e4) || next
        v.is_almost_prime(n) || next
        if (v.is_squarefree) {
            return k
        }
    }
}

assert_eq(
    A281940.map(1..12),
    %n[1, 3, 9, 43, 46, 47, 245, 1697, 109, 565, 3938, 3255]
)

say "** Test passed!"
