#!/usr/bin/ruby

#
## https://rosettacode.org/wiki/Sorting_algorithms/Merge_sort
#

func merge(left, right) {
    var result = [];
    while (!left.is_empty && !right.is_empty) {
        result.append([right,left][left.first <= right.first].shift);
    };
    result + left + right;
}
 
func mergesort(array) {
    var len = array.len;
    len < 2 && return array;
 
    var mid   = (len/2 -> int);
    var left  = array.slice(0, mid);
    var right = array.slice(mid);
 
    left  = __FUNC__(left);
    right = __FUNC__(right);
 
    merge(left, right);
}
 
# Numeric sort
var nums = @(0..7).shuffle;
var s1 = mergesort(nums)
say s1
assert_eq(s1, nums.sort)
 
# String sort
var strings = @('a'..'e').shuffle;
var s2 = mergesort(strings)
say s2
assert_eq(s2, strings.sort)
