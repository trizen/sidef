#!/usr/bin/ruby

#
## http://rosettacode.org/wiki/Ulam_spiral_(for_primes)
#

require('Imager');
require('ntheory');

var (n=512, start=1, file='ulam.png') = ARGV»to_i»()...;

func cell(n, x, y, start) {
    y -= (n   >> 1);
    x -= (n-1 >> 1);
    var l = 2*(x.abs > y.abs ? x.abs : y.abs);
    var d = (y > x  ? (l*3 + x + y) : (l - x - y));
    (l-1)**2 + d + start - 1;
}

var black = %s'Imager::Color'.new('#000000');
var white = %s'Imager::Color'.new('#FFFFFF');

var img = %s'Imager'.new(xsize => n, ysize => n, channels => 1);
img.box(filled => 1, color => white);

(n-1).range.each { |y|
    (n-1).range.each { |x|
        var v = cell(n, x, y, start);
        %S'ntheory'.is_prime(v) && (
            img.setpixel(x => x, y => y, color => black)
        );
    }
}

img.write(file => file);
