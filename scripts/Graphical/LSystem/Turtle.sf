#!/usr/bin/ruby

require('Image::Magick');

class Turtle(
    x      = 500,
    y      = 500,
    angle  = 0,
    mirror = 1,
) {

    has im = %s<Image::Magick>.new(size => "#{x}x#{y}")

    method init {
        angle.deg2rad!
        im.ReadImage('canvas:white')
    }

    method forward(r, opt) {
        var (newx, newy) = (x + r*sin(angle), y + r*-cos(angle))

        im.Draw(
            primitive => 'line',
            points    => join(' ',
                           int(x    * opt{:scale} + opt{:xoff}),
                           int(y    * opt{:scale} + opt{:yoff}),
                           int(newx * opt{:scale} + opt{:xoff}),
                           int(newy * opt{:scale} + opt{:yoff}),
                        ),
            stroke      => opt{:color},
            strokewidth => 1,
        )

        (x, y) = (newx, newy)
    }

    method save_as(filename) {
        im.Write(filename)
    }

    method turn(theta) {
        angle += theta*mirror
    }

    method state {
        [x, y, angle, mirror]
    }

    method setstate(state) {
        (x, y, angle, mirror) = state...
    }

    method mirror {
        mirror.neg!
    }
}
