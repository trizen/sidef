#!/usr/bin/perl

#
## qw() with comments and space-escapes
#

var array = qw(
    a b c
    hello\ world       # space escaped
    value(i)           # this is a comment
    \#not_a_comment
);

array == [
    'a', 'b', 'c',
    'hello world',
    'value(i)',
    '#not_a_comment'
] || (
    "qw() error!\n".die
);

#
## Double quoted words
#

qqw <
    tab:\t
    newline:\n
    not\ a\ new_line!:\\n
> == [
    "tab:\t",
    "newline:\n",
    'not a new_line!:\n'
] || (
    "qqw<> error!\n".die
);

#
## Balanced and escaped delimiter
#

var single_q = q «Single quo\ted s«tr»ing: Escaping: \\\««\»» \»\» »;

single_q == 'Single quo\ted s«tr»ing: Escaping: \««»» »» '
    || ("q«» error!\n".die);


#
## Escaped unary delimited
#

single_q = q~hello\~world~;

single_q == 'hello~world'
    || ("q~~ error!\n".die);


#
## Backslash delimiter (q\\)
#

var double_q = qq\#not a comment\;

double_q == '#not a comment'
    || ("qq\\\\ error!\n".die);


#
## Balanced brackets
#

q{g\~o{{\}\{\}{\{}}\\}o\}\{{da\}g}le} ==
 'g\~o{{}{}{{}}\}o}{{da}g}le'        || ("q{} error!".die);


#
## Real-quote delimiter
#

„test\n” == "test\n"
    || ("„” error!\n".die);

#
## Unescaping
#

double_q = qq „tab:\t -- not a tab: \\t”;
double_q == ('tab:' + "\t" + ' -- not a tab: \\t')
    || ("qq„” error!\n".die);


"All tests passed!".say;
